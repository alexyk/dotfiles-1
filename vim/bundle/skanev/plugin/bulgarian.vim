" Some support for writing Bulgarian.
"
" I exit insert mode with 'jk', but in my Cyrillic keymap, this is a common
" letter sequence (тн). Thus, whenever I am writing text, I often accidentally
" exit when trying to type 'относно' or 'вероятно'. To avoid it, I check if
" the word before the cursor is a prefix of a Bulgarian dictionary word. If
" so, I let Vim enter 'тн'. Otherwise it exits to normal mode.
"
" Here's a small Ruby contraption to generate the dictionary prefixes:
"
"     words = `aspell -l bg dump master | grep -F 'тн'`
"     p words.map { |w| w[/^.*тн/, 0].chomp('тн') }.uniq.sort
"
" For speed, I have inlined the dictionary in this file instead of shelling
" out to aspell every time. It currently performs a linear lookup, but I find
" this quick enough.

let s:prefixes = [
      \ "абсолю", "абстрак", "авторите", "адеква", "азо", "акура", "амбивален",
      \ "ароган", "безвъзвра", "безграмо", "безде", "безжалос", "беззаве", "беззащи",
      \ "безизвес", "безкорис", "безмилос", "безотче", "безплъ", "безпредме",
      \ "безпристрас", "безпъ", "безрадос", "безрезулта", "безропо", "безсмър",
      \ "безстрас", "безсъвес", "безучас", "безцве", "безчес", "би", "бла",
      \ "благоприя", "бу", "буревес", "вален", "вероя", "вес", "взаимоо", "вие",
      \ "визи", "вирулен", "влас", "вме", "вра", "връ", "връс", "всеизвес",
      \ "всеобхва", "гавръ", "галан", "глу", "глъ", "гос", "градоби", "грамо", "гъ",
      \ "дванадесетопръс", "двукра", "деве", "делика", "десе", "дефек", "дискре",
      \ "добросъвес", "доминан", "е", "едновръс", "еквивален", "екзак", "екстраваган",
      \ "елеган", "жи", "живо", "жлъ", "завис", "зага", "зали", "заме", "замес",
      \ "запре", "заси", "защи", "злопаме", "злощас", "зна", "зрелос", "извес",
      \ "изле", "изме", "иманен", "имо", "импозан", "импотен", "индиферен", "инер",
      \ "интелиген", "ирелеван", "кле", "клеве", "коке", "колори", "компак",
      \ "компетен", "комплек", "конверген", "конкре", "консеквен", "констан", "корек",
      \ "корпулен", "кос", "кохерен", "кра", "крепос", "кръс", "културнопросве",
      \ "културпросве", "къ", "ланце", "латен", "ле", "ли", "лис", "луминесцен", "лю",
      \ "ля", "магни", "малограмо", "ме", "мес", "металорабо", "ми", "мимоле",
      \ "многокра", "моноли", "мъ", "надвлас", "надкос", "налбан", "наме", "намес",
      \ "напръс", "народовлас", "насе", "наси", "наус", "неабсолю", "неадеква",
      \ "неакура", "неблагоприя", "небу", "невероя", "неграмо", "неделика",
      \ "недискре", "недобросъвес", "неизвес", "некомпак", "некомпетен", "некорек",
      \ "ненавис", "ненаси", "нео", "необхва", "необя", "неопе", "неопи", "неотга",
      \ "непобу", "непокъ", "непоня", "непохва", "неприве", "неприя", "несвес",
      \ "несвяс", "несме", "неспре", "несре", "несъо", "несъпричас", "нетолеран",
      \ "неумес", "неую", "нечес", "нечистоплъ", "нещас", "о", "обек", "обра",
      \ "обхва", "общоизвес", "опе", "опи", "отве", "отга", "отме", "отри", "отсе",
      \ "отче", "охо", "пакос", "паме", "пар", "пе", "пера", "перманен", "перфек",
      \ "пес", "петолис", "печа", "пикан", "пла", "плъ", "по", "побу", "повра",
      \ "повсемес", "повърхнос", "подбу", "подвлас", "подлакъ", "подлис", "подме",
      \ "подотче", "подпре", "подри", "позага", "покъ", "политрабо", "полузащи",
      \ "поме", "помъ", "поня", "пос", "посве", "потен", "похва", "превра", "преглъ",
      \ "предвес", "предме", "прели", "преме", "пресме", "прилис", "природозащи",
      \ "присве", "пристрас", "прихва", "прицве", "прия", "прокле", "проле", "просве",
      \ "противоспъ", "пъ", "първоби", "ра", "рабо", "радиовес", "развра", "разноцве",
      \ "редундан", "резистен", "резулта", "ри", "руга", "само", "самоби", "самовлас",
      \ "све", "свес", "свяс", "се", "секре", "си", "ски", "скорос", "слабограмо",
      \ "сме", "сму", "смъ", "смър", "спир", "спле", "спре", "спъ", "стенвес", "сто",
      \ "столе", "страс", "суе", "съве", "съвес", "съзакля", "съо", "съотве",
      \ "съпричас", "съпъ", "съра", "съучас", "темперамен", "тоале", "толеран",
      \ "трилис", "тъ", "тютюнорабо", "умес", "уплъ", "ус", "уста", "учас", "ую",
      \ "фи", "фреквен", "хлъ", "хобо", "хубос", "цве", "цялос", "ча", "час", "че",
      \ "чернорабо", "чес", "четирилис", "чис", "чистоплъ", "човеконенавис", "чу",
      \ "ше", "шеме", "шес"]

function! s:HeadLine()
  return strpart(getline("."), 0, col(".")-1)
endfunction

function! s:HeadWord()
  return matchstr(s:HeadLine(), '\k\+$')
endfunction

function! BulgarianJK()
  if !&iminsert || index(s:prefixes, tolower(s:HeadWord())) == -1
    return "\<Esc>"
  else
    return 'тн'
  endif
endfunction
